<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" xmlns="*">

	<mx:Script>
		<![CDATA[
			import insync.ContactEvent;
			import insync.Contact;
			import insync.ContactForm;

			import mx.rpc.events.FaultEvent;
			import mx.controls.Alert;
			import mx.collections.ArrayCollection;
			import mx.rpc.events.ResultEvent;
			
			[Bindable] private var contacts:ArrayCollection;

			private function resultHandler(event:ResultEvent):void
			{
				contacts = event.result as ArrayCollection
			}

			private function faultHandler(event:FaultEvent):void
			{
				Alert.show(event.fault.faultDetail);
			}

			public function openContact(contact:Contact):void
			{
				var children:Array = tn.getChildren();
				for (var i:int = 0; i<children.length; i++)
				{
					if (ContactForm(children[i]).contact.id == contact.id)
					{
						tn.selectedChild = children[i];
						return;
					}
				}
				
				var form:ContactForm = new ContactForm();
				form.addEventListener(ContactEvent.CREATED, contactCreatedHandler);
				form.addEventListener(ContactEvent.UPDATED, contactUpdatedHandler);
				form.addEventListener(ContactEvent.DELETED, contactDeletedHandler);
				tn.addChild(form);
				form.contact = contact;
				tn.selectedChild = form;
			}

			private function search():void
			{
				ro.findByName(searchStr.text);
			}

			private function contactCreatedHandler(event:ContactEvent):void
			{
				search();				
			}

			private function contactUpdatedHandler(event:ContactEvent):void
			{
				search();				
			}

			private function contactDeletedHandler(event:ContactEvent):void
			{
				tn.removeChild(event.target as ContactForm);
				search();				
			}

		]]>
	</mx:Script>

	<mx:RemoteObject id="ro" destination="contactService">
		<mx:method name="findByName" result="resultHandler(event)"/>
	</mx:RemoteObject>

	<mx:ApplicationControlBar width="100%">
		<mx:TextInput id="searchStr"/>
		<mx:Button label="Search" click="search()"/>
		<mx:Button label="New Contact" click="openContact(new Contact())"/>
	</mx:ApplicationControlBar>

	<mx:HDividedBox width="100%" height="100%">
		<mx:DataGrid id="dg" dataProvider="{contacts}" width="30%" height="100%"
			doubleClickEnabled="true"
			doubleClick="openContact(dg.selectedItem as Contact)">
			<mx:columns>
				<mx:DataGridColumn dataField="firstName" headerText="First Name"/>
				<mx:DataGridColumn dataField="lastName" headerText="Last Name"/>
			</mx:columns>
		</mx:DataGrid>
		<mx:TabNavigator id="tn" width="70%" height="100%"/>
	</mx:HDividedBox>

</mx:Application>